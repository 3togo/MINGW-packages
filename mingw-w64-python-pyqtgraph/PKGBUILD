# Maintainer: ciciwu <yw@m-labs.hk>

_realname=pyqtgraph
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.12.4
pkgrel=2
pkgdesc='Scientific Graphics and GUI Library for Python.'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.pyqtgraph.org/"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-pyqt5" "${MINGW_PACKAGE_PREFIX}-python" "${MINGW_PACKAGE_PREFIX}-python-numpy" "${MINGW_PACKAGE_PREFIX}-python-pyopengl"
$([[ "$_enable_scipy" == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-python-scipy"))
# "${MINGW_PACKAGE_PREFIX}-python-scipy" is optional
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://github.com/pyqtgraph/pyqtgraph/archive/refs/tags/pyqtgraph-${pkgver}.tar.gz")
sha256sums=('5b3140b0f2efc4e821fe101baf72279e277b3d216cbd65ba0195c7ffb4029394')

prepare() {
  cd "${srcdir}"

  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}-${_realname}-${pkgver}" "python-build-${CARCH}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  msg "Python build for ${CARCH}"  
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

#check() {
#  msg "Python test for ${CARCH}"
#  cd "${srcdir}/python-build-${CARCH}"
#  PYTHONPATH=. ${MINGW_PREFIX}/bin/pytest .
#}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"

}
