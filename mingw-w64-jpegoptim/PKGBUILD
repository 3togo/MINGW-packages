# Maintainer: Ray Griffin <rorgoroth-dev@outlook.com>

_realname=jpegoptim
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.7
pkgrel=1
pkgdesc="Utility to optimize jpeg files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.kokkonen.net/tjko/projects.html"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-libjpeg-turbo")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/tjko/jpegoptim/archive/refs/tags/v${pkgver}.tar.gz
        https://github.com/tjko/jpegoptim/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz.asc)
sha256sums=('c52616f2fb8d481315871680f9943b0f58c553d1e0c49a6bd4691a3e66d7e6de'
            'SKIP')
validpgpkeys=('9168CD7A2F0AAB0679D09BBA9D08A80CED908D6A') # Timo Kokkonen <tjko@iki.fi>

build() {
  cd "${_realname}-${pkgver}"

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
