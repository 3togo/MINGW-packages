# Maintainer: yumetodo <yume-wikijp@live.jp>

_realname=hexyl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.0
pkgrel=1
pkgdesc="A command-line hex viewer"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://github.com/sharkdp/hexyl"
license=('spdx:Apache-2.0' 'spdx:MIT')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=(
  "${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
)
sha512sums=('e5f22d8d3d6d49c2f0643972d33b290029f8a02f33a7c731420ae1621ba9a0736bfc888538b793383cb59cc28487ec14b65ad40cab9a93de04e0208573e0e117')
b2sums=('1e264db4047bbde7d665f566beeda464485350ca03ef2dffaafe7ffb941666858ddab3796f8fec22097f4fd7ac0f071bed7a5dd550759969e838a2acb6e65324')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm 755 "target/release/${_realname}" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}"
  install -Dm 644 LICENSE* -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
