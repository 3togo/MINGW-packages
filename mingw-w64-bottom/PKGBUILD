# Maintainer: Wu Zhenyu <wuzy01@qq.com>
_realname=bottom

pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=0.6.8
pkgrel=1
pkgdesc="A graphical process/system monitor (mingw-w64)"
url="https://github.com/ClementTsang/bottom"
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64)
license=('MIT')
makedepends=($MINGW_PACKAGE_PREFIX-rust)
source=("$url/archive/$pkgver/$_realname-$pkgver.tar.gz")
sha512sums=('e414b9c8ce4c5c27ac1f3ac6d29a1dcca2c269b5d6d60d90408324635bf69f5f50947c5ff2dff38fc123939c508c18cb144b14f96d4fd581962770a9b49544dc')
b2sums=('942bafdcec64327d14ebcd70bf445262d3f7458ea951ecd8e36e749524065b68ddfa1ed85bdcb95bb114aad8a53d8db053b55145f0b9be23d9890f45ec06864d')

prepare() {
  cd "$_realname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$_realname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$_realname-$pkgver"
  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --frozen --all-features
}

package() {
  cd "$_realname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir$MINGW_PREFIX/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir$MINGW_PREFIX/share/licenses/$_realname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir$MINGW_PREFIX/share/doc/$_realname" *.md
  cp -vr docs sample_configs "$pkgdir$MINGW_PREFIX/share/doc/$_realname"

  # completions
  local target_dir=$(ls target/release/build/bottom-*/out/btm.bash | head -n1 | xargs dirname)
  install -Dm644 "$target_dir"/_btm "$pkgdir$MINGW_PREFIX/share/zsh/site-functions/_btm"
  install -Dm644 "$target_dir"/btm.bash "$pkgdir$MINGW_PREFIX/share/bash-completion/completions/btm"
  install -Dm644 "$target_dir"/btm.fish "$pkgdir$MINGW_PREFIX/share/fish/vendor_completions.d/btm.fish"
}
