# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# contributers: Zach Bacon <zachbacon@budgiemedia.rocks>

_realname=SDL2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.22
pkgrel=2
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2) (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://libsdl.org"
license=("spdx:Zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-vulkan")
options=('staticlibs' 'strip')
source=("https://libsdl.org/release/SDL2-${pkgver}.tar.gz"
        001-fix-cmake-target-relocation.patch
        002-fix-link-order.patch
        005-sdl-config-win-paths.patch
        006-workaround-winrt-gaming-input.patch)
sha256sums=('fe7cbf3127882e3fc7259a75a0cb585620272c51745d3852ab9dd87960697f2e'
            'ecb28a00b57c3d29485bc14a3fd798044801aaaaf5781c8d02a47dc29f610193'
            '619d56f1996cb2d2cff0e806843175fd5ccd94607796ec271f46a5338710d97d'
            '258b2e90da4bd38f61fab37f35cee25b84a45c4bb25d27bc0c05f57ab26d7d64'
            '096bfadebd30af69bb15b231ed47a778d571485af7cc2b696337f0be31453e62')
validpgpkeys=('1528635D8053A57F77D1E08630A59377A7763BE6') # Sam Lantinga

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  # Fixes sdl-config https://github.com/msys2/MINGW-packages/issues/9881
  patch -p1 -i ${srcdir}/001-fix-cmake-target-relocation.patch
  patch -p1 -i ${srcdir}/002-fix-link-order.patch
  patch -p1 -i ${srcdir}/005-sdl-config-win-paths.patch

  # Reported https://github.com/libsdl-org/SDL/issues/5589
  patch -p1 -i ${srcdir}/006-workaround-winrt-gaming-input.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DSDL_VULKAN=ON \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -vDm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
