# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=transmission
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.00
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.transmissionbt.com/'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-libevent"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-miniupnpc")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-libutp"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "intltool")
options=('strip' 'staticlibs')
source=("https://github.com/transmission/transmission-releases/raw/master/transmission-${pkgver}.tar.xz"
        "https://raw.githubusercontent.com/transmission/transmission/${pkgver}/po/pt_PT.po" # missing in tarball
        "skip-broken-cmake-rc.patch")
sha256sums=('9144652fe742f7f7dd6657716e378da60b751aaeda8bef8344b3eefc4db255f2'
            'f96b4b03d977fadca51716bcecc51582c980a7d5f1e1519b97837955f5fae59e'
            '7cdf69c16bcc3966e40e2ff237d98488c6606e3396270691f0fb7263a287c498')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/skip-broken-cmake-rc.patch
  cp "${srcdir}/pt_PT.po" po/
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # TODO: package more needed libraries instead of including them.
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DENABLE_TESTS=OFF \
      -DENABLE_QT=OFF \
      -DENABLE_GTK=ON \
      -DUSE_SYSTEM_EVENT2=ON \
      -DUSE_SYSTEM_DHT=OFF \
      -DUSE_SYSTEM_MINIUPNPC=ON \
      -DUSE_SYSTEM_NATPMP=OFF \
      -DUSE_SYSTEM_UTP=ON \
      -DUSE_SYSTEM_B64=OFF \
      -DWITH_CRYPTO=openssl \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}
