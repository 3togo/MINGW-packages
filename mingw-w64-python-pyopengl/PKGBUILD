# Maintainer: Antoine Martin <totaam@xpra.org>

_realname=pyopengl
_pyname=PyOpenGL
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_pyname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_pyname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_pyname}")
pkgver=3.1.5
pkgrel=4
pkgdesc="PyOpenGL is the most common cross platform Python binding to OpenGL and related APIs (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://pyopengl.sourceforge.io/'
license=('PyOpenGL')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc")
options=('staticlibs' 'strip' '!debug')
source=("https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz"
        "https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}-accelerate/${_pyname}-accelerate-${pkgver}.tar.gz")
sha256sums=('4107ba0d0390da5766a08c242cf0cf3404c377ed293c5f6d701e457c57ba3424'
            '12e5518b0216a478527c7ce5ddce623c3d0517adeb87226da767772e8b7f2f06')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH} | true
  cp -r "${_pyname}-${pkgver}" "python-build-${CARCH}"
  cp -r "${_pyname}-accelerate-${pkgver}" "python-build-${CARCH}-accelerate"
  echo "TODO" > "python-build-${CARCH}/readme.rst"
  echo "TODO" > "python-build-${CARCH}-accelerate/readme.rst"
}

build() {
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build --force
  msg "accelerate module:"
  cd "${srcdir}/python-build-${CARCH}-accelerate"
  ${MINGW_PREFIX}/bin/python setup.py build --force
}

check() {
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py check
  cd "${srcdir}/python-build-${CARCH}-accelerate"
  ${MINGW_PREFIX}/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 license.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING"

  cd "${srcdir}/python-build-${CARCH}-accelerate"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
