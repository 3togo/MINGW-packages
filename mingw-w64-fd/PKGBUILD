# Maintainer: Wu Zhenyu <wuzy01@qq.com>
_realname=fd

pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=8.3.2
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find (mingw-w64)'
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64)
url=https://github.com/sharkdp/fd
makedepends=("$MINGW_PACKAGE_PREFIX-rust")
license=(MIT APACHE)
source=("$url/archive/v$pkgver.tar.gz")
sha512sums=('83778d86ef1ea3e00ba40c7dca5fd743a9eb72362a57fb964d808a6919d0bbb2b42439c03107f98fe534dd0977eb9e414e4232afb808abe9994d70f2a81d925b')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  install -Dm755 target/release/fd "$pkgdir"$MINGW_PREFIX/bin/fd
  install -Dm644 "$(find -regex './target/release/build/fd-find-.*/out/fd.bash'|head -n1)" "$pkgdir"$MINGW_PREFIX/share/bash-completion/completions/fd
  install -Dm644 "$(find -regex './target/release/build/fd-find-.*/out/fd.fish'|head -n1)" "$pkgdir"$MINGW_PREFIX/share/fish/vendor_completions.d/fd.fish
  install -Dm644 contrib/completion/_fd "$pkgdir"$MINGW_PREFIX/share/zsh/site-functions/_fd
  install -Dm644 doc/fd.1 "$pkgdir"$MINGW_PREFIX/share/man/man1/fd.1
  install -Dm644 LICENSE-APACHE "$pkgdir"$MINGW_PREFIX/share/licenses/fd/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"$MINGW_PREFIX/share/licenses/fd/LICENSE-MIT
}
